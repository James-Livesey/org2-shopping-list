SLADD:
LOCAL NAME$(255),K%,F%,M%,PRICE$(6),PRIO$(4)
START::
USE A
CURSOR ON
DO
 CLS
 IF K%<>4 :FIRST :ENDIF
 F%=FIND(NAME$)
 IF F%>0
  AT 1,2 :PRINT LEFT$(A.NAME$,16)
 ENDIF
 AT 1,1 :PRINT RIGHT$("NAME: "+NAME$,15)
 K%=GET
 IF K%=1 AND NAME$=""
  CURSOR OFF
  RETURN
 ELSEIF K%=1 :NAME$=""
 ELSEIF K%>13 :NAME$=NAME$+CHR$(K%)
 ELSEIF K%=8 AND NAME$<>"" :NAME$=LEFT$(NAME$,LEN(NAME$)-1)
 ELSEIF K%=2
  M%=MENU("EDIT,APPEND,ERASE")
  IF M%=1 :GOTO EDIT::
  ELSEIF M%=2 :GOTO APPEND::
  ELSEIF M%=3 :GOTO ERASE::
  ENDIF
  CURSOR ON
 ELSEIF K%=4 :NEXT
 ENDIF
UNTIL K%=13
IF EOF
 APPEND::
 CURSOR OFF
 IF NAME$="" :RETURN :ENDIF
 A.NAME$=NAME$
 A.DESC$=""
 A.PRICE=-1
 A.PRIO%=0
 APPEND
ENDIF
CURSOR OFF
USE B
B.NAME$=A.NAME$
B.DESC$=A.DESC$
B.PRICE=A.PRICE
B.QTY=1
B.PRIO%=A.PRIO%
APPEND
RETURN

EDIT::
IF F%=0 :GOTO START:: :ENDIF
CLS
PRINT "NAME: ";
EDIT A.NAME$
IF A.NAME$="": GOTO START:: :ENDIF
PRINT "DESC: ";
EDIT A.DESC$
KSTAT 3
PRINT "PRICE: ";CHR$(0);
IF A.PRICE>=0
 PRICE$=FIX$(A.PRICE,2,6)
ELSE
 PRICE$=""
ENDIF
EDIT PRICE$
A.PRICE=-1
ONERR UNKNPRCE::
A.PRICE=VAL(PRICE$)
UNKNPRCE::
ONERR OFF
PRIO$=GEN$(A.PRIO%,4)
PRINT "PRIO: ";
EDIT PRIO$
ONERR UNKNPRIO::
A.PRIO%=VAL(PRIO$)
UNKNPRIO::
ONERR OFF
UPDATE
KSTAT 1
GOTO START::

ERASE::
IF F%=0 :GOTO START:: :ENDIF
CURSOR OFF
CLS
PRINT "ERASE ITEM"
PRINT "ARE YOU SURE Y/N"
IF GET=%Y
 ERASE
ENDIF
GOTO START::