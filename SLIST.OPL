SLIST:
LOCAL K%,M%,I%
LOCAL POOLF$(10),ITEMSF$(10)
LOCAL UNAME$(255),UDESC$(255),UPRICE,UQTY,UPRIO%
START::
POOLF$="A:SLPOOL"
ITEMSF$="A:SLITEMS"
UDG:(0,6,9,8,12,8,24,31,0)
IF NOT EXIST(POOLF$)
 CREATE POOLF$,A,NAME$,DESC$,PRICE,PRIO%
ELSE
 OPEN POOLF$,A,NAME$,DESC$,PRICE,PRIO%
ENDIF
IF NOT EXIST(ITEMSF$)
 CREATE ITEMSF$,B,NAME$,DESC$,PRICE,QTY,PRIO%
ELSE
 OPEN ITEMSF$,B,NAME$,DESC$,PRICE,QTY,PRIO%
ENDIF
RENDER::
USE B
CLS
IF EOF
 PRINT REPT$("*",18);"END OF LIST***"
 K%=GET
ELSE
 IF B.PRICE>=0
  AT 1,2 :PRINT CHR$(0);FIX$(B.PRICE,2,5)
 ELSE
  AT 1,2 :PRINT CHR$(0);CHR$(63)
 ENDIF
 AT 8,2 :PRINT "*";NUM$(B.QTY,2)
 AT 12,2 :PRINT NUM$(POS,-2);"/";NUM$(COUNT,2)
 K%=VIEW(1,B.NAME$)
ENDIF
IF K%=1 :STOP
ELSEIF K%=2
 M%=MENU("ADD,UNDO,DEFER,SORT,CLEAR,RESET")
 IF M%=1 :SLADD:
 ELSEIF M%=2
  IF UNAME$=""
   CLS
   PRINT "NOTHING TO UNDO"
   GET
  ELSE
   B.NAME$=UNAME$
   B.DESC$=UDESC$
   B.PRICE=UPRICE
   B.QTY=UQTY
   B.PRIO%=UPRIO%
   APPEND
   UNAME$=""
  ENDIF
 ELSEIF M%=3
  I%=POS
  UPDATE
  POSITION I%
 ELSEIF M%=4 :SLSORT:
 ELSEIF M%=5
  CLS
  PRINT "CLEAR LIST"
  PRINT "ARE YOU SURE Y/N"
  IF GET=%Y
   GOTO CLEAR::
  ENDIF
 ELSEIF M%=6
  CLS
  PRINT "RESET ALL DATA"
  PRINT "ARE YOU SURE Y/N"
  IF GET=%Y
   GOTO RESET::
  ENDIF
 ENDIF
ELSEIF K%=3 :BACK
ELSEIF K%=4 :NEXT
ELSEIF K%=13 AND NOT EOF
 UNAME$=B.NAME$
 UDESC$=B.DESC$
 UPRICE=B.PRICE
 UQTY=B.QTY
 UPRIO%=B.PRIO%
 ERASE
 IF EOF :BACK :ENDIF
ELSEIF K%=%L OR K%=%* :GOTO QTY::
ENDIF
GOTO RENDER::

QTY::
IF EOF :GOTO RENDER:: :ENDIF
AT 9,2 :INPUT B.QTY
UPDATE
GOTO RENDER::

CLEAR::
CLS
PRINT "CLEARING..."
USE A :CLOSE
USE B :CLOSE
DELETE ITEMSF$
GOTO START::

RESET::
CLS
PRINT "RESETTING..."
USE A :CLOSE
USE B :CLOSE
DELETE POOLF$
DELETE ITEMSF$
GOTO START::